model {
  # group level priors
  mu_alpha ~ dbeta(2, 4)T(0.001,)
  mu_theta ~ dnorm(0.4, 4)T(0,1)
  mu_delta ~ dnorm(0.5, 4)T(0,1)
  mu_phi ~   dnorm(0, 0.4)
  mu_beta ~  dt(0, 1, 1)T(0.001,) #student t distribution
  
  lambda_alpha ~ dgamma(0.005, 0.01)T(0.001,)
  lambda_theta ~ dgamma(0.005,  0.01)T(0.001,)
  lambda_delta ~ dgamma(0.005,  0.01)T(0.001,)
  lambda_phi ~   dgamma(0.01,  0.01)T(0.001,)
  lambda_beta ~  dgamma(0.005,  0.01)T(0.001,)
  
  for (s in 1:nsubs){
    alpha[s] ~ dnorm(mu_alpha, lambda_alpha)T(0, 1)
    theta[s] ~ dnorm(mu_theta, lambda_theta)T(0, 1)
    delta[s] ~ dnorm(mu_delta, lambda_delta)T(0, 1)  # sd = 1/4
    phi[s] ~ dnorm(mu_phi, lambda_phi)
    beta[s] ~ dnorm(mu_beta, lambda_beta)T(0,)
  
      # choice first round
    for (d in 1:4){
      explore[s, 1,d] ~ dnorm(0, 1)
      exploit[s, 1,d] ~ dnorm(0, 1)

      p[s, 1,d] <- 0.25
    }
    
    for (t in 2:ntrials_all[s]){  # for each trial
      
      # calculate 'utility'
      v[s, t] <- X[s, t-1]^theta[s] - Y[s, t-1]^theta[s]
      
      for (d in 1:4){ # for each deck
        exploit[s, t, d] <- ifelse(x[s, t-1] == d,
                                exploit[s, t-1, d] * delta[s] + v[s, t],
                                exploit[s, t-1, d] * delta[s])
        explore[s, t, d] <- explore[s, t-1, d] + alpha[s] * (phi[s] - explore[s, t-1, d])
        
        # for softmax
        tmp_p[s, t, d] <- exp(beta[s] * (exploit[s, t, d] + explore[s, t, d]))
      }
  
      # update prop
      for (d in 1:4){
        p[s, t,d] <- tmp_p[s, t,d]/sum(tmp_p[s, t,1:4])
      }
      x[s, t] ~ dcat(p[s, t,]) # categorical distribution
    }
  }
}