model {

  #w ~ dnorm(0,1)T(0,)
  #A ~ dnorm(0,1)T(0,1) #both at the bottom and at the top - almost uniform; they can't be risk seeking for gains, and risk averse for losses #(assuming prospect theory)
  #theta ~ dnorm(0,1)T(0,)
  #a ~ dnorm(0,1)T(0,1) #uniform but for calculation reasons we do this
  
  # setting priors 
  w ~ dt(0, 0.25, 1)T(0,) # half cauchy with a precision of 0.25 (mean 0, 1 means cauchy as opposed to student's t)
  A ~ dt(0, 0.25, 1)T(0,)
  theta ~ dnorm(0,1)T(0,)
  a ~ dnorm(0,1)T(0,1)
  
  # gut feelings
  Ev[1,1] ~ dnorm(0, .1)
  Ev[1,2] ~ dnorm(0, .1)
  Ev[1,3] ~ dnorm(0, .1)
  Ev[1,4] ~ dnorm(0, .1)
  
  #deterministic on which card gets chosen 1st, could put a prior on it as well
  p[1,1] <- .25
  p[1,2] <- .25
  p[1,3] <- .25
  p[1,4] <- .25

  for (t in 2:ntrials) {
    
    for (d in 1:4) {
      
      u[t,d] <- ifelse(X[t-1] < 0, -w * abs(X[t-1])^A, abs(X[t-1])^A)
      
      Ev_update[t,d] <- Ev[t-1, d] + (a * (u[t,d] - Ev[t-1,d]))
      
      Ev[t,d] <- ifelse(x[t-1] == d, Ev_update[t,d], Ev[t-1,d])
      
      exp_p[t,d] <- exp(theta * Ev[t,d])
      
      # FIX: IN CASE THE ^A IS APPROACHING INFINITY
      #if(is.infinite(exp_p[t,d])){exp_p[t,d] = exp(500)}
      
      }
    
    for (d in 1:4) {
      p[t,d] <- exp_p[t,d] / sum(exp_p[t,])
    }
    
    x[t] ~ dcat(p[t,]) #we want the whole sample dist
    
  }
}